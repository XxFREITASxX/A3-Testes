import java_cup.runtime.*;

terminal String LINHA, COLUNA, PECA, POSICAO,
                CAPTURA, XEQUE, XEQUEMATE, PROMOCAO,
                ROQUE_GRANDE, ROQUE_PEQUENO,
                NUMERO_TURNO,
                VITORIA_BRANCAS, VITORIA_PRETAS, EMPATE;

non terminal partida, resultado, jogadas, lance, jogada,
             movimento, movimentoPeao, movimentoPeca,
             promocao_opcional, sufixo,
             roque, desambiguidade;

start with partida;

partida ::= jogadas resultado;

resultado ::=
      /* vazio */
    | VITORIA_BRANCAS     /* 1-0 */
    | VITORIA_PRETAS      /* 0-1 */
    | EMPATE              /* 1/2-1/2 */
;

jogadas ::= 
      lance
    | jogadas lance
;

lance ::=
      NUMERO_TURNO jogada jogada
    | NUMERO_TURNO jogada
;

jogada ::=
      movimento sufixo
    | roque sufixo
;

movimento ::= 
      movimentoPeao
    | movimentoPeca
;

movimentoPeao ::= 
      POSICAO promocao_opcional
    | COLUNA CAPTURA POSICAO promocao_opcional
;

movimentoPeca ::= 
     PECA desambiguidade POSICAO
    | PECA desambiguidade CAPTURA POSICAO
;

desambiguidade ::=
    /* vazio */
  | COLUNA
  | LINHA
  | COLUNA LINHA
;

promocao_opcional ::= 
      /* vazio */
    | PROMOCAO PECA
;

sufixo ::= 
      /* vazio */
    | XEQUE
    | XEQUEMATE
;

roque ::=
      ROQUE_GRANDE
    | ROQUE_PEQUENO
;