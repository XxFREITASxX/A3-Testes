import java_cup.runtime.*;

terminal String LINHA, COLUNA, PECA,
                CAPTURA, XEQUE, XEQUEMATE, PROMOCAO,
                ROQUE_GRANDE, ROQUE_PEQUENO,
                NUMERO_TURNO,
                VITORIA_BRANCAS, VITORIA_PRETAS, EMPATE;

non terminal partida, resultado, jogadas, lance, jogada,
             jogada_sem_final, jogada_com_final,
             movimento_base, movimentoPeao, movimentoPeca,
             promocao_opcional, xeque_opcional,
             roque, posicao;

start with partida;

partida ::= jogadas resultado;

resultado ::=
      /* vazio */
    | VITORIA_BRANCAS     /* 1-0 */
    | VITORIA_PRETAS      /* 0-1 */
    | EMPATE              /* 1/2-1/2 */
;

jogadas ::= 
      lance
    | jogadas lance
;

lance ::=
      NUMERO_TURNO jogada jogada
    | NUMERO_TURNO jogada
;

jogada ::=
      jogada_sem_final
    | jogada_com_final
;

jogada_sem_final ::= 
      movimento_base xeque_opcional
    | roque xeque_opcional
;

jogada_com_final ::= 
      movimento_base XEQUEMATE
    | roque XEQUEMATE
;

movimento_base ::= 
      movimentoPeao
    | movimentoPeca
;

movimentoPeao ::= 
      posicao promocao_opcional
    | COLUNA CAPTURA posicao promocao_opcional
;

movimentoPeca ::= 
      PECA posicao
    | PECA LINHA posicao
    | PECA COLUNA posicao
    | PECA CAPTURA posicao
    | PECA LINHA CAPTURA posicao
    | PECA COLUNA CAPTURA posicao
;

promocao_opcional ::= 
      /* vazio */
    | PROMOCAO PECA
;

xeque_opcional ::= 
      /* vazio */
    | XEQUE
;

roque ::=
      ROQUE_GRANDE
    | ROQUE_PEQUENO
;

posicao ::= COLUNA LINHA;